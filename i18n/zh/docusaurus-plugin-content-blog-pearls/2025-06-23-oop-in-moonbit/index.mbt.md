---
description: 'MoonBitä¸­çš„é¢å‘å¯¹è±¡ç¼–ç¨‹'
slug: oop-in-moonbit
image: cover.png
tags: [MoonBit, Pearls]
authors: åˆ˜å­æ‚¦
---

# MoonBitä¸­çš„é¢å‘å¯¹è±¡ç¼–ç¨‹

![alt text](./cover.png)

## å¼•è¨€

åœ¨è½¯ä»¶å¼€å‘çš„ä¸–ç•Œé‡Œï¼Œé¢å‘å¯¹è±¡ç¼–ç¨‹ï¼ˆOOPï¼‰æ— ç–‘æ˜¯ä¸€åº§ç»•ä¸å¼€çš„è¯é¢˜ã€‚Javaã€C++ ç­‰è¯­è¨€å‡­å€Ÿå…¶å¼ºå¤§çš„ OOP æœºåˆ¶æ„å»ºäº†æ— æ•°å¤æ‚çš„ç³»ç»Ÿã€‚ç„¶è€Œï¼ŒMoonbitï¼Œä½œä¸ºä¸€é—¨å›´ç»•å‡½æ•°å¼ç¼–ç¨‹æ„å»ºçš„ç°ä»£è¯­è¨€ï¼Œå®ƒå¦‚ä½•å®ç° OOPï¼Ÿ

Moonbit æ˜¯ä¸€é—¨ä»¥å‡½æ•°å¼ç¼–ç¨‹ä¸ºæ ¸å¿ƒçš„è¯­è¨€ï¼Œå®ƒçš„é¢å‘å¯¹è±¡ç¼–ç¨‹æ€è·¯ä¸ä¼ ç»Ÿç¼–ç¨‹è¯­è¨€æœ‰å¾ˆå¤§ä¸åŒã€‚å®ƒæŠ›å¼ƒäº†ä¼ ç»Ÿçš„ç»§æ‰¿æœºåˆ¶ï¼Œæ‹¥æŠ±"ç»„åˆä¼˜äºç»§æ‰¿"çš„è®¾è®¡å“²å­¦ã€‚ä¹ä¸€çœ‹ï¼Œè¿™å¯èƒ½è®©ä¹ æƒ¯äº†ä¼ ç»ŸOOPçš„ç¨‹åºå‘˜æœ‰äº›ä¸é€‚åº”ï¼Œä½†ç»†ç»†å“å‘³ï¼Œä½ ä¼šå‘ç°è¿™ç§æ–¹æ³•æœ‰ç€æ„æƒ³ä¸åˆ°çš„ä¼˜é›…å’Œå®ç”¨æ€§ã€‚

æœ¬æ–‡å°†é€šè¿‡ä¸€ä¸ªç”ŸåŠ¨çš„RPGæ¸¸æˆå¼€å‘ä¾‹å­ï¼Œå¸¦ä½ æ·±å…¥ä½“éªŒMoonbitä¸­çš„é¢å‘å¯¹è±¡ç¼–ç¨‹ã€‚æˆ‘ä»¬ä¼šé€ä¸€å‰–æå°è£…ã€ç»§æ‰¿å’Œå¤šæ€è¿™ä¸‰å¤§ç‰¹æ€§ï¼Œå¹¶ä¸C++çš„å®ç°æ–¹å¼è¿›è¡Œå¯¹æ¯”ï¼Œæœ€åæä¾›ä¸€äº›å®é™…å¼€å‘ä¸­çš„æœ€ä½³å®è·µå»ºè®®ã€‚

## å°è£…ï¼ˆEncapsulationï¼‰

æƒ³è±¡ä¸€ä¸‹ï¼Œæˆ‘ä»¬è¦å¼€å‘ä¸€æ¬¾ç»å…¸çš„å•æœºRPGæ¸¸æˆã€‚åœ¨è¿™ä¸ªå¥‡å¹»ä¸–ç•Œé‡Œï¼Œè‹±é›„å››å¤„æ¸¸å†ï¼Œä¸æ€ªç‰©æˆ˜æ–—ï¼Œå‘NPCå•†äººè´­ä¹°è£…å¤‡ï¼Œæœ€ç»ˆæ‹¯æ•‘è¢«å›°çš„å…¬ä¸»ã€‚è¦æ„å»ºè¿™æ ·ä¸€ä¸ªä¸–ç•Œï¼Œæˆ‘ä»¬é¦–å…ˆéœ€è¦å¯¹å…¶ä¸­çš„æ‰€æœ‰å…ƒç´ è¿›è¡Œå»ºæ¨¡ã€‚

ä¸ç®¡æ˜¯å‹‡æ•¢çš„è‹±é›„ã€å‡¶æ¶çš„æ€ªç‰©ï¼Œè¿˜æ˜¯æœ´å®çš„æ¡Œæ¤…æ¿å‡³ï¼Œå®ƒä»¬åœ¨æ¸¸æˆä¸–ç•Œä¸­éƒ½æœ‰ä¸€äº›å…±åŒçš„ç‰¹å¾ã€‚æˆ‘ä»¬å¯ä»¥å°†è¿™äº›å¯¹è±¡éƒ½æŠ½è±¡ä¸º`Sprite`ï¼ˆç²¾çµï¼‰ï¼Œæ¯ä¸ªSpriteéƒ½åº”è¯¥å…·å¤‡å‡ ä¸ªåŸºæœ¬å±æ€§ï¼š

- `ID`ï¼šå¯¹è±¡çš„å”¯ä¸€æ ‡è¯†ç¬¦ï¼Œå°±åƒèº«ä»½è¯å·ç ä¸€æ ·ã€‚
- `x`å’Œ`y`ï¼šåœ¨æ¸¸æˆåœ°å›¾ä¸Šçš„åæ ‡ä½ç½®ã€‚

### C++çš„ç»å…¸å°è£…æ–¹å¼

åœ¨C++çš„ä¸–ç•Œé‡Œï¼Œæˆ‘ä»¬ä¹ æƒ¯äºç”¨`class`æ¥æ„å»ºæ•°æ®çš„å°è£…ï¼š

```cpp
// ä¸€ä¸ªåŸºç¡€çš„ Sprite ç±»
class Sprite {
private:
    int id;
    double x;
    double y;

public:
    // æ„é€ å‡½æ•°ï¼Œç”¨æ¥åˆ›å»ºå¯¹è±¡
    Sprite(int id, double x, double y) : id(id), x(x), y(y) {}

    // æä¾›ä¸€äº›å…¬å…±çš„ "getter" æ–¹æ³•æ¥è®¿é—®æ•°æ®
    int getID() const { return id; }
    double getX() const { return x; }
    double getY() const { return y; }

    // å¯èƒ½è¿˜éœ€è¦ "setter" æ–¹æ³•æ¥ä¿®æ”¹æ•°æ®
    void setX(double newX) { x = newX; }
    void setY(double newY) { y = newY; }
};
```

ä½ å¯èƒ½ä¼šé—®ï¼š"ä¸ºä»€ä¹ˆè¦æè¿™ä¹ˆå¤š`get`æ–¹æ³•ï¼Œç›´æ¥æŠŠå±æ€§è®¾ä¸º`public`ä¸å°±å¥½äº†ï¼Ÿ"è¿™å°±æ¶‰åŠåˆ°å°è£…çš„æ ¸å¿ƒæ€æƒ³äº†ã€‚

> **ä¸ºä»€ä¹ˆéœ€è¦å°è£…ï¼Ÿ**
>
> æƒ³è±¡ä¸€ä¸‹ï¼Œå¦‚æœä½ çš„åŒäº‹ç›´æ¥é€šè¿‡`sprite.id = enemy_id`æ¥ä¿®æ”¹IDï¼Œè‹±é›„ç¬é—´å°±èƒ½"å˜èº«"æˆæ•Œäººçš„åŒä¼™ï¼Œç›´æ¥å¤§æ‘‡å¤§æ‘†åœ°èµ°åˆ°ç»ˆç‚¹â€”â€”ä½†è¿™æ˜¾ç„¶ä¸æ˜¯æˆ‘ä»¬æƒ³è¦çš„æ¸¸æˆæœºåˆ¶ï¼å°è£…å°±åƒç»™æ•°æ®åŠ äº†ä¸€é“é˜²æŠ¤ç½‘ï¼Œ`private`å­—æ®µé…åˆ`getter`æ–¹æ³•ï¼Œç¡®ä¿å¤–éƒ¨åªèƒ½è¯»å–è€Œæ— æ³•éšæ„ä¿®æ”¹å…³é”®æ•°æ®ã€‚è¿™æ ·çš„è®¾è®¡è®©ä»£ç æ›´åŠ å¥å£®ï¼Œé¿å…äº†æ„æƒ³ä¸åˆ°çš„å‰¯ä½œç”¨ã€‚

### Moonbitçš„ä¼˜é›…å°è£…

åˆ°äº†Moonbitè¿™é‡Œï¼Œå°è£…çš„æ€è·¯å‘ç”Ÿäº†å¾®å¦™è€Œé‡è¦çš„å˜åŒ–ã€‚è®©æˆ‘ä»¬å…ˆçœ‹ä¸€ä¸ªç®€å•çš„ç‰ˆæœ¬ï¼š

```moonbit no check
// åœ¨ Moonbit ä¸­å®šä¹‰ Sprite
pub struct Sprite {
  id: Int          // é»˜è®¤ä¸å¯å˜ï¼Œå¤–éƒ¨å¯è¯»ä½†ä¸å¯å†™
  mut x: Double    // mut å…³é”®å­—è¡¨ç¤ºå¯å˜
  mut y: Double
}

// æˆ‘ä»¬å¯ä»¥ä¸º struct å®šä¹‰æ–¹æ³•
pub fn Sprite::get_x(self: Self) -> Double {
  self.x
}

pub fn Sprite::get_y(self: Self) -> Double {
  self.y
}

pub fn Sprite::set_x(self: Self, new_x: Double) -> Unit {
  self.x = new_x
}

pub fn Sprite::set_y(self: Self, new_y: Double) -> Unit {
  self.y = new_y
}
```

æ³¨æ„åˆ°è¿™é‡Œæœ‰ä¸¤ä¸ªå…³é”®çš„ä¸åŒç‚¹ï¼š

**1. å¯å˜æ€§çš„æ˜¾å¼å£°æ˜**

åœ¨Moonbitä¸­ï¼Œå­—æ®µé»˜è®¤æ˜¯**ä¸å¯å˜**çš„ï¼ˆimmutableï¼‰ã€‚å¦‚æœä½ æƒ³è®©æŸä¸ªå­—æ®µå¯ä»¥è¢«ä¿®æ”¹ï¼Œå¿…é¡»æ˜ç¡®ä½¿ç”¨`mut`å…³é”®å­—ã€‚åœ¨æˆ‘ä»¬çš„`Sprite`ä¸­ï¼Œ`id`ä¿æŒä¸å¯å˜â€”â€”è¿™å®Œç¾ç¬¦åˆæˆ‘ä»¬çš„è®¾è®¡æ„å›¾ï¼Œæ¯•ç«Ÿæˆ‘ä»¬ä¸å¸Œæœ›å¯¹è±¡çš„èº«ä»½è¢«éšæ„ç¯¡æ”¹ã€‚è€Œ`x`å’Œ`y`è¢«æ ‡è®°ä¸º`mut`ï¼Œå› ä¸ºç²¾çµéœ€è¦åœ¨ä¸–ç•Œä¸­è‡ªç”±ç§»åŠ¨ã€‚

**2. æ›´ç®€æ´çš„è®¿é—®æ§åˆ¶**

ç”±äº`id`æœ¬èº«å°±æ˜¯ä¸å¯å˜çš„ï¼Œæˆ‘ä»¬ç”šè‡³ä¸éœ€è¦ä¸ºå®ƒç¼–å†™`get_id`æ–¹æ³•ï¼å¤–éƒ¨ä»£ç å¯ä»¥ç›´æ¥é€šè¿‡`sprite.id`æ¥è¯»å–å®ƒï¼Œä½†ä»»ä½•å°è¯•ä¿®æ”¹çš„è¡Œä¸ºéƒ½ä¼šè¢«ç¼–è¯‘å™¨åšå†³æ‹’ç»ã€‚è¿™æ¯”C++çš„"private + getter"æ¨¡å¼æ›´åŠ ç®€æ´æ˜äº†ï¼ŒåŒæ—¶ä¿æŒäº†åŒæ ·çš„å®‰å…¨æ€§ã€‚

> **ğŸ’¡ å®è·µå»ºè®®**
>
> åœ¨è®¾è®¡æ•°æ®ç»“æ„æ—¶ï¼Œä¼˜å…ˆè€ƒè™‘å“ªäº›å­—æ®µçœŸæ­£éœ€è¦å¯å˜ã€‚Moonbitçš„é»˜è®¤ä¸å¯å˜è®¾è®¡èƒ½å¸®ä½ é¿å…å¾ˆå¤šæ„å¤–çš„çŠ¶æ€ä¿®æ”¹bugã€‚

## ç»§æ‰¿ï¼ˆInheritanceï¼‰

é¢å‘å¯¹è±¡ç¼–ç¨‹çš„ç¬¬äºŒå¤§æ”¯æŸ±æ˜¯ç»§æ‰¿ã€‚åœ¨æˆ‘ä»¬çš„RPGä¸–ç•Œä¸­ï¼Œä¼šæœ‰å¤šç§ä¸åŒç±»å‹çš„Spriteã€‚ä¸ºäº†ç®€åŒ–ç¤ºä¾‹ï¼Œæˆ‘ä»¬å®šä¹‰ä¸‰ç§ï¼š

- `Hero`ï¼ˆè‹±é›„ï¼‰ï¼šç©å®¶æ“æ§çš„è§’è‰²
- `Enemy`ï¼ˆæ•Œäººï¼‰ï¼šéœ€è¦è¢«å‡»è´¥çš„å¯¹æ‰‹
- `Merchant`ï¼ˆå•†äººï¼‰ï¼šå”®å–é“å…·çš„NPC

### C++çš„ç»§æ‰¿å±‚æ¬¡

åœ¨C++ä¸­ï¼Œæˆ‘ä»¬å¾ˆè‡ªç„¶åœ°ä½¿ç”¨ç±»ç»§æ‰¿æ¥æ„å»ºè¿™ç§å±‚çº§å…³ç³»ï¼š

```cpp
class Hero : public Sprite {
private:
    double hp;
    double damage;
    int money;

public:
    Hero(int id, double x, double y, double hp, double damage, int money)
        : Sprite(id, x, y), hp(hp), damage(damage), money(money) {}

    void attack(Enemy& e) { /* ... */ }
};

class Enemy : public Sprite {
private:
    double hp;
    double damage;

public:
    Enemy(int id, double x, double y, double hp, double damage)
        : Sprite(id, x, y), hp(hp), damage(damage) {}

    void attack(Hero& h) { /* ... */ }
};

class Merchant : public Sprite {
public:
    Merchant(int id, double x, double y) : Sprite(id, x, y) {}
    // å•†äººä¸“æœ‰çš„æ–¹æ³•...
};
```

C++çš„é¢å‘å¯¹è±¡å»ºç«‹åœ¨ **"is-a"** å…³ç³»åŸºç¡€ä¸Šï¼š`Hero`**æ˜¯ä¸€ä¸ª**`Sprite`ï¼Œ`Enemy`**æ˜¯ä¸€ä¸ª**`Sprite`ã€‚è¿™ç§æ€ç»´æ–¹å¼ç›´è§‚ä¸”å®¹æ˜“ç†è§£ã€‚

### Moonbitçš„ç»„åˆå¼æ€ç»´

ç°åœ¨è½®åˆ°Moonbitäº†ã€‚è¿™é‡Œéœ€è¦è¿›è¡Œä¸€æ¬¡é‡è¦çš„æ€ç»´è½¬æ¢ï¼š**Moonbitçš„`struct`ä¸æ”¯æŒç›´æ¥ç»§æ‰¿**ã€‚å–è€Œä»£ä¹‹çš„æ˜¯ä½¿ç”¨`trait`ï¼ˆç‰¹è´¨ï¼‰å’Œ**ç»„åˆ**ï¼ˆCompositionï¼‰ã€‚

è¿™ç§è®¾è®¡è¿«ä½¿æˆ‘ä»¬é‡æ–°æ€è€ƒé—®é¢˜ï¼šæˆ‘ä»¬ä¸å†å°†`Sprite`è§†ä¸ºå¯è¢«ç»§æ‰¿çš„"çˆ¶ç±»"ï¼Œè€Œæ˜¯å°†å…¶æ‹†åˆ†ä¸ºä¸¤ä¸ªç‹¬ç«‹çš„æ¦‚å¿µï¼š

1. **`SpriteData`**ï¼šä¸€ä¸ªçº¯ç²¹çš„æ•°æ®ç»“æ„ï¼Œå­˜å‚¨æ‰€æœ‰Spriteå…±äº«çš„æ•°æ®
2. **`Sprite`**ï¼šä¸€ä¸ªtraitï¼Œå®šä¹‰æ‰€æœ‰Spriteåº”è¯¥å…·å¤‡çš„è¡Œä¸ºèƒ½åŠ›

è®©æˆ‘ä»¬çœ‹çœ‹å®é™…çš„ä»£ç ï¼š

```moonbit no check
// 1. å®šä¹‰å…±äº«çš„æ•°æ®ç»“æ„
pub struct SpriteData {
  id: Int
  mut x: Double
  mut y: Double
}

// 2. å®šä¹‰æè¿°é€šç”¨è¡Œä¸ºçš„ Trait
pub trait Sprite {
  getSpriteData(Self) -> SpriteData  // å¿…é¡»å®ç°çš„æ ¸å¿ƒæ–¹æ³•
  getID(Self) -> Int = _             // = _ è¡¨ç¤ºæœ‰é»˜è®¤å®ç°
  getX(Self) -> Double = _
  getY(Self) -> Double = _
  setX(Self, Double) -> Unit = _
  setY(Self, Double) -> Unit = _
}

// Spriteçš„é»˜è®¤å®ç°
// åªè¦å®ç°äº† getSpriteDataï¼Œå°±è‡ªåŠ¨æ‹¥æœ‰äº†å…¶ä»–æ–¹æ³•
impl Sprite with getID(self) {
  self.getSpriteData().id
}

impl Sprite with getX(self) {
  self.getSpriteData().x
}

impl Sprite with getY(self) {
  self.getSpriteData().y
}

impl Sprite with setX(self, new_x) {
  self.getSpriteData().x = new_x
}

impl Sprite with setY(self, new_y) {
  self.getSpriteData().y = new_y
}
```

> **ç†è§£Traitçš„å¨åŠ›**
>
> `Sprite` traitå®šä¹‰äº†ä¸€ä¸ª"å¥‘çº¦"ï¼šä»»ä½•å£°ç§°è‡ªå·±æ˜¯`Sprite`çš„ç±»å‹ï¼Œéƒ½å¿…é¡»èƒ½å¤Ÿæä¾›å®ƒçš„`SpriteData`ã€‚ä¸€æ—¦æ»¡è¶³äº†è¿™ä¸ªæ¡ä»¶ï¼Œ`getID`ã€`getX`ã€`getY`ç­‰æ–¹æ³•å°±ä¼šè‡ªåŠ¨å¯ç”¨ã€‚è¿™é‡Œçš„`= _`è¯­æ³•è¡¨ç¤ºè¯¥æ–¹æ³•æœ‰é»˜è®¤å®ç°ï¼Œè¿™æ˜¯Moonbitçš„æœ€æ–°è¯­æ³•ç‰¹æ€§ã€‚

æœ‰äº†è¿™ä¸ªåŸºç¡€æ¶æ„ï¼Œæˆ‘ä»¬å°±å¯ä»¥å®ç°å…·ä½“çš„æ¸¸æˆè§’è‰²äº†ï¼š

```moonbit
// å®šä¹‰Hero
pub struct Hero {
  sprite_data: SpriteData  // ç»„åˆSpriteData
  hp: Double
  damage: Int
  money: Int
}

// å®ç°Sprite traitï¼Œåªéœ€è¦æä¾›getSpriteDataæ–¹æ³•
pub impl Sprite for Hero with getSpriteData(self) {
  self.sprite_data
}

pub fn Hero::attack(self: Self, e: Enemy) -> Unit {
  // æ”»å‡»é€»è¾‘...
}

// å®šä¹‰Enemy
pub struct Enemy {
  sprite_data: SpriteData
  hp: Double
  damage: Int
}

pub impl Sprite for Enemy with getSpriteData(self) {
  self.sprite_data
}

pub fn Enemy::attack(self: Self, h: Hero) -> Unit {
  // æ”»å‡»é€»è¾‘...
}

// å®šä¹‰Merchant
pub struct Merchant {
  sprite_data: SpriteData
}

pub impl Sprite for Merchant with getSpriteData(self) {
  self.sprite_data
}
```

æ³¨æ„è¿™é‡Œçš„æ€ç»´æ–¹å¼è½¬å˜ï¼šMoonbité‡‡ç”¨çš„æ˜¯ **"has-a"** å…³ç³»ï¼Œè€Œä¸æ˜¯ä¼ ç»ŸOOPçš„ **"is-a"** å…³ç³»ã€‚`Hero`**æ‹¥æœ‰**`SpriteData`ï¼Œå¹¶ä¸”**å®ç°**äº†`Sprite`çš„èƒ½åŠ›ã€‚

> **çœ‹èµ·æ¥Moonbitæ›´å¤æ‚ï¼Ÿ**
>
> åˆçœ‹ä¹‹ä¸‹ï¼ŒMoonbitçš„ä»£ç ä¼¼ä¹æ¯”C++è¦å†™æ›´å¤š"æ¨¡æ¿ä»£ç "ã€‚ä½†è¿™åªæ˜¯è¡¨é¢ç°è±¡ï¼æˆ‘ä»¬è¿™é‡Œåˆ»æ„å›é¿äº†C++çš„è¯¸å¤šå¤æ‚æ€§ï¼šæ„é€ å‡½æ•°ã€ææ„å‡½æ•°ã€constæ­£ç¡®æ€§ã€æ¨¡æ¿å®ä¾‹åŒ–ç­‰ç­‰ã€‚æ›´é‡è¦çš„æ˜¯ï¼ŒMoonbitè¿™ç§è®¾è®¡åœ¨å¤§å‹é¡¹ç›®ä¸­ä¼šå±•ç°å‡ºå·¨å¤§ä¼˜åŠ¿â€”â€”æˆ‘ä»¬ç¨åä¼šè¯¦ç»†è®¨è®ºè¿™ä¸€ç‚¹ã€‚

## å¤šæ€ï¼ˆPolymorphismï¼‰

å¤šæ€æ˜¯é¢å‘å¯¹è±¡ç¼–ç¨‹çš„ç¬¬ä¸‰å¤§æ”¯æŸ±ï¼ŒæŒ‡çš„æ˜¯**åŒä¸€ä¸ªæ¥å£ä½œç”¨äºä¸åŒå¯¹è±¡æ—¶äº§ç”Ÿä¸åŒè¡Œä¸º**çš„èƒ½åŠ›ã€‚è®©æˆ‘ä»¬é€šè¿‡ä¸€ä¸ªå…·ä½“ä¾‹å­æ¥ç†è§£ï¼šå‡è®¾æˆ‘ä»¬éœ€è¦å®ç°ä¸€ä¸ª`who_are_you`å‡½æ•°ï¼Œå®ƒèƒ½å¤Ÿè¯†åˆ«ä¼ å…¥å¯¹è±¡çš„ç±»å‹å¹¶ç»™å‡ºç›¸åº”å›ç­”ã€‚

### C++çš„å¤šæ€æœºåˆ¶

C++çš„å¤šæ€æœºåˆ¶å®é™…ä¸Šæ˜¯ä¸€ä¸ªæ¯”è¾ƒå¤æ‚çš„é—®é¢˜ï¼Œç¬¼ç»Ÿåœ°è¯´ï¼Œå®ƒåŒ…æ‹¬é™æ€å¤šæ€ï¼ˆæ¨¡æ¿ï¼‰å’ŒåŠ¨æ€å¤šæ€ï¼ˆè™šå‡½æ•°ã€RTTIç­‰ï¼‰ã€‚å¯¹C++å¤šæ€æœºåˆ¶çš„è®¨è®ºè¶…å‡ºäº†æˆ‘ä»¬è¿™ç¯‡æ–‡ç« çš„å†…å®¹èŒƒå›´ï¼Œè¯»è€…å¦‚æœæœ‰å…´è¶£å¯ä»¥è‡ªè¡ŒæŸ¥é˜…ç›¸å…³ä¹¦ç±ã€‚è¿™é‡Œæˆ‘ä»¬é‡ç‚¹è®¨è®ºä¸¤ç§ç»å…¸çš„è¿è¡Œæ—¶å¤šæ€æ–¹æ³•ã€‚

#### æ–¹æ³•ä¸€ï¼šè™šå‡½æ•°æœºåˆ¶

æœ€ä¼ ç»Ÿçš„åšæ³•æ˜¯ä¸ºåŸºç±»å®šä¹‰è™šå‡½æ•°ï¼Œè®©å­ç±»é‡å†™ï¼š

```cpp
class Sprite {
public:
    virtual ~Sprite() = default;  // è™šææ„å‡½æ•°
    // å®šä¹‰ä¸€ä¸ª"çº¯è™šå‡½æ•°"ï¼Œå¼ºåˆ¶å­ç±»å¿…é¡»å®ç°å®ƒ
    virtual std::string say_name() const = 0;
};

// åœ¨å­ç±»ä¸­"é‡å†™"(override)è¿™ä¸ªå‡½æ•°
class Hero : public Sprite {
public:
    std::string say_name() const override {
        return "I am a hero!";
    }
    // ...
};

class Enemy : public Sprite {
public:
    std::string say_name() const override {
        return "I am an enemy!";
    }
    // ...
};

class Merchant : public Sprite {
public:
    std::string say_name() const override {
        return "I am a merchant.";
    }
    // ...
};

// ç°åœ¨ who_are_you å‡½æ•°å˜å¾—æå…¶ç®€å•ï¼
void who_are_you(const Sprite& s) {
    std::cout << s.say_name() << std::endl;
}
```

#### æ–¹æ³•äºŒï¼šRTTI + dynamic_cast

å¦‚æœæˆ‘ä»¬ä¸æƒ³ä¸ºæ¯ä¸ªç±»å•ç‹¬å®šä¹‰è™šå‡½æ•°ï¼Œè¿˜å¯ä»¥ä½¿ç”¨C++çš„è¿è¡Œæ—¶ç±»å‹ä¿¡æ¯ï¼ˆRTTIï¼‰ï¼š

```cpp
class Sprite {
public:
    // æ‹¥æœ‰è™šå‡½æ•°çš„ç±»æ‰èƒ½ä½¿ç”¨ RTTI
    virtual ~Sprite() = default;
};

// who_are_you å‡½æ•°çš„å®ç°
void who_are_you(const Sprite& s) {
    if (dynamic_cast<const Hero*>(&s)) {
        std::cout << "I am a hero!" << std::endl;
    } else if (dynamic_cast<const Enemy*>(&s)) {
        std::cout << "I am an enemy!" << std::endl;
    } else if (dynamic_cast<const Merchant*>(&s)) {
        std::cout << "I am a merchant." << std::endl;
    } else {
        std::cout << "I don't know who I am" << std::endl;
    }
}
```

> **RTTIçš„å·¥ä½œåŸç†**
>
> å¼€å¯RTTIåï¼ŒC++ç¼–è¯‘å™¨ä¼šä¸ºæ¯ä¸ªæœ‰è™šå‡½æ•°çš„å¯¹è±¡ç»´æŠ¤ä¸€ä¸ªéšå¼çš„`type_info`ç»“æ„ã€‚å½“ä½¿ç”¨`dynamic_cast`æ—¶ï¼Œç¼–è¯‘å™¨æ£€æŸ¥è¿™ä¸ªç±»å‹ä¿¡æ¯ï¼šåŒ¹é…åˆ™è¿”å›æœ‰æ•ˆæŒ‡é’ˆï¼Œä¸åŒ¹é…åˆ™è¿”å›`nullptr`ã€‚è¿™ç§æœºåˆ¶è™½ç„¶åŠŸèƒ½å¼ºå¤§ï¼Œä½†ä¹Ÿå¸¦æ¥äº†è¿è¡Œæ—¶å¼€é”€ã€‚

ä¸è¿‡ï¼Œç¬¬äºŒç§æ–¹æ³•åœ¨å¤§å‹é¡¹ç›®ä¸­å­˜åœ¨ä¸€äº›é—®é¢˜ï¼š

1. **ç±»å‹ä¸å®‰å…¨**ã€‚å¦‚æœä½ æ–°å¢äº†ä¸€ä¸ªå­ç±»ä½†å¿˜è®°ä¿®æ”¹`who_are_you`å‡½æ•°ï¼Œè¿™ä¸ªbugåªèƒ½åœ¨è¿è¡Œæ—¶æ‰èƒ½è¢«å‘ç°ï¼åœ¨ç°ä»£è½¯ä»¶å¼€å‘ä¸­ï¼Œæˆ‘ä»¬æ›´å¸Œæœ›æ­¤ç±»é”™è¯¯èƒ½åœ¨ç¼–è¯‘æ—¶å°±è¢«æ•è·ã€‚
2. **æ€§èƒ½ä¸å¤Ÿå¥½**ã€‚å¼€å¯RTTIåï¼Œæ¯ä¸€æ¬¡åˆ¤æ–­ç±»å‹éƒ½ä¼šè°ƒç”¨ä¸€ä¸ªæ¯”è¾ƒéº»çƒ¦çš„ç±»å‹ä¿¡æ¯è¯»å–æ–¹æ³•ï¼Œè¿™ä¸å¤ªåˆ©äºä¼˜åŒ–ï¼Œå› æ­¤å¾ˆå®¹æ˜“å‡ºç°æ€§èƒ½ä¸Šçš„é—®é¢˜ã€‚
3. **æ•°æ®ä¸é€æ˜**ã€‚å¼€å¯RTTIåï¼ŒC++ä¼šä¸ºæ¯ä¸€ä¸ªç±»éšå¼åœ°æ·»åŠ ä¸€å—ç±»å‹ä¿¡æ¯ï¼Œä½†æ˜¯ä»£ç çš„ç¼–å†™è€…æ˜¯çœ‹ä¸åˆ°çš„ï¼Œè¿™å¯¹äºä¸€äº›æœŸæœ›å¯¹ä»£ç æ‹¥æœ‰æ›´å¼ºæŒæ§åŠ›çš„åº“ç¼–å†™è€…è€Œè¨€éå¸¸å¤´ç–¼ã€‚äº‹å®ä¸Šï¼Œä¸å°‘å¤§å‹é¡¹ç›®ä¼šè€ƒè™‘ç¦ç”¨RTTIï¼Œæœ€å…¸å‹çš„å°±æ˜¯LLVMï¼Œè¿™ä¸ªC++çš„ç¼–è¯‘å™¨é¡¹ç›®åè€Œè‡ªå·±å¹¶ä¸æ„¿æ„ä½¿ç”¨RTTI.

### Moonbitçš„ADTæœºåˆ¶

Moonbité€šè¿‡å¼•å…¥**ä»£æ•°æ•°æ®ç±»å‹**ï¼ˆAlgebraic Data Typeï¼ŒADTï¼‰æ¥ä¼˜é›…åœ°è§£å†³å¤šæ€é—®é¢˜ã€‚æˆ‘ä»¬éœ€è¦æ·»åŠ ä¸€ä¸ªæ–°çš„ç»“æ„â€”â€”`SpriteEnum`ï¼š

```moonbit no check
pub trait Sprite {
  getSpriteData(Self) -> SpriteData
  asSpriteEnum(Self) -> SpriteEnum  // æ–°å¢ï¼šç±»å‹è½¬æ¢æ–¹æ³•
}

// Moonbitå…è®¸enumçš„æ ‡ç­¾åå’Œç±»åé‡å
pub enum SpriteEnum {
  Hero(Hero)
  Enemy(Enemy)
  Merchant(Merchant)
}

// æˆ‘ä»¬ä»ç„¶éœ€è¦å®ç°Spriteä¸­çš„getSpriteData
pub impl Sprite for Hero with getSpriteData(self) {
  self.sprite_data
}

pub impl Sprite for Enemy with getSpriteData(self) {
  self.sprite_data
}

pub impl Sprite for Merchant with getSpriteData(self) {
  self.sprite_data
}

// ä¸ºä¸‰ä¸ªå­ç±»å®ç° asSpriteEnum æ–¹æ³•
// è¿™é‡Œå®é™…ä¸Šæ˜¯å°†å…·ä½“ç±»å‹"è£…ç®±"åˆ°enumä¸­
pub impl Sprite for Hero with asSpriteEnum(self) {
  Hero(self)  // æ³¨æ„ï¼šè¿™é‡Œçš„Heroæ˜¯enumæ ‡ç­¾ï¼Œä¸æ˜¯ç±»å‹
}

pub impl Sprite for Enemy with asSpriteEnum(self) {
  Enemy(self)
}

pub impl Sprite for Merchant with asSpriteEnum(self) {
  Merchant(self)
}
```

ç°åœ¨æˆ‘ä»¬å¯ä»¥å®ç°ç±»å‹å®‰å…¨çš„`who_are_you`å‡½æ•°äº†ï¼š

```moonbit no check
test "who are you" {
  fn who_are_you(s: &Sprite) -> String {
    // ä½¿ç”¨æ¨¡å¼åŒ¹é…è¿›è¡Œç±»å‹åˆ†å‘
    match s.asSpriteEnum() {
      Hero(_) => "hero"
      Enemy(_) => "enemy"
      Merchant(_) => "merchant"
    }
  }

  let hero = Hero::new();
  let enemy = Enemy::new();
  let merchant = Merchant::new();
  inspect(who_are_you(hero), content="hero")
  inspect(who_are_you(enemy), content="enemy")
  inspect(who_are_you(merchant), content="merchant")
}
```

è¿™ç§æ–¹æ³•çš„ç¾å¦™ä¹‹å¤„åœ¨äºï¼š**å®ƒæ˜¯ç¼–è¯‘æ—¶ç±»å‹å®‰å…¨çš„**ï¼å¦‚æœä½ æ·»åŠ äº†ä¸€ä¸ªæ–°çš„`Sprite`å­ç±»ä½†å¿˜è®°ä¿®æ”¹`who_are_you`å‡½æ•°ï¼Œç¼–è¯‘å™¨ä¼šç«‹å³æŠ¥é”™ï¼Œè€Œä¸æ˜¯ç­‰åˆ°è¿è¡Œæ—¶æ‰å‘ç°é—®é¢˜ã€‚

> **é™æ€åˆ†å‘ vs åŠ¨æ€åˆ†å‘**
>
> ä½ å¯èƒ½æ³¨æ„åˆ°å‡½æ•°ç­¾åä¸­çš„`&Sprite`ã€‚è¿™åœ¨Moonbitä¸­è¢«ç§°ä¸º**Trait Object**ï¼Œæ”¯æŒåŠ¨æ€åˆ†å‘ï¼Œç±»ä¼¼äºC++çš„è™šå‡½æ•°æœºåˆ¶ã€‚å¦‚æœä½ å†™æˆ`fn[S: Sprite] who_are_you(s: S)`ï¼Œé‚£å°±æ˜¯é™æ€åˆ†å‘ï¼ˆæ³›å‹ï¼‰ï¼Œç¼–è¯‘å™¨ä¼šä¸ºæ¯ç§å…·ä½“ç±»å‹ç”Ÿæˆä¸“é—¨çš„ä»£ç ã€‚
>
> ä¸¤è€…çš„å…³é”®åŒºåˆ«åœ¨äºå¤„ç†**å¼‚æ„é›†åˆ**çš„èƒ½åŠ›ã€‚å‡è®¾è‹±é›„æœ‰AOEæŠ€èƒ½éœ€è¦æ”»å‡»ä¸€ä¸ªåŒ…å«ä¸åŒç±»å‹æ•Œäººçš„æ•°ç»„ï¼Œä½ å¿…é¡»ä½¿ç”¨`Array[&Sprite]`è€Œä¸æ˜¯`Array[V]`ï¼Œå› ä¸ºåè€…æ— æ³•åŒæ—¶å®¹çº³ä¸åŒçš„å…·ä½“ç±»å‹ã€‚

å½“ç„¶ï¼ŒMoonbitä¹Ÿæ”¯æŒç±»ä¼¼C++è™šå‡½æ•°çš„ç›´æ¥æ–¹æ³•è°ƒç”¨ï¼š

```moonbit no check
pub trait SayName {
  say_name(Self) -> String
}

pub impl SayName for Hero with say_name(_) {
  "hero"
}

pub impl SayName for Enemy with say_name(_) {
  "enemy"
}

pub impl SayName for Merchant with say_name(_) {
  "merchant"
}

test "say_name" {
  fn who_are_you(s: &SayName) -> String {
    s.say_name()  // ç›´æ¥è°ƒç”¨traitæ–¹æ³•ï¼Œç±»ä¼¼è™šå‡½æ•°
  }

  let hero = Hero::new();
  let enemy = Enemy::new();
  let merchant = Merchant::new();
  inspect(who_are_you(hero), content="hero")
  inspect(who_are_you(enemy), content="enemy")
  inspect(who_are_you(merchant), content="merchant")
}
```

> **æ˜¾å¼åŒ–çš„RTTI**
>
> å®é™…ä¸Šï¼ŒMoonbitçš„ADTæ–¹æ³•å°±æ˜¯å°†C++éšå¼çš„RTTIè¿‡ç¨‹æ˜¾å¼åŒ–äº†ã€‚å¼€å‘è€…æ˜ç¡®çŸ¥é“æœ‰å“ªäº›ç±»å‹ï¼Œç¼–è¯‘å™¨ä¹Ÿèƒ½åœ¨ç¼–è¯‘æ—¶è¿›è¡Œå®Œæ•´æ€§æ£€æŸ¥ã€‚

## å¤šå±‚ç»§æ‰¿ï¼šæ„å»ºå¤æ‚çš„èƒ½åŠ›ä½“ç³»

éšç€æ¸¸æˆç³»ç»Ÿçš„å‘å±•ï¼Œæˆ‘ä»¬å‘ç°`Hero`å’Œ`Enemy`éƒ½æœ‰`hp`ï¼ˆç”Ÿå‘½å€¼ï¼‰ã€`damage`ï¼ˆæ”»å‡»åŠ›ï¼‰å’Œ`attack`æ–¹æ³•ã€‚èƒ½å¦å°†è¿™äº›å…±åŒç‰¹å¾æŠ½è±¡å‡ºæ¥ï¼Œå½¢æˆä¸€ä¸ª`Warrior`ï¼ˆæˆ˜å£«ï¼‰å±‚çº§å‘¢ï¼Ÿ

### C++çš„å¤šå±‚ç»§æ‰¿

åœ¨C++ä¸­ï¼Œæˆ‘ä»¬å¯ä»¥å¾ˆè‡ªç„¶åœ°åœ¨ç»§æ‰¿é“¾ä¸­æ’å…¥æ–°çš„ä¸­é—´å±‚ï¼š

```cpp
class Warrior : public Sprite {
protected: // ä½¿ç”¨ protectedï¼Œå­ç±»å¯ä»¥è®¿é—®
    double hp;
    double damage;

public:
    Warrior(int id, double x, double y, double hp, double damage)
        : Sprite(id, x, y), hp(hp), damage(damage) {}

    virtual void attack(Sprite& target) = 0; // æˆ˜å£«éƒ½èƒ½æ”»å‡»

    double getHP() const { return hp; }
    double getDamage() const { return damage; }
};

class Hero final : public Warrior {
    private:
        int money;
    public:
        Hero(int id, double x, double y, double hp, double damage, int money)
            : Warrior(id, x, y, hp, damage), money(money) {}
};

class Enemy final : public Warrior {
    public:
        Enemy(int id, double x, double y, double hp, double damage)
            : Warrior(id, x, y, hp, damage) {}
};

class Merchant final : public Sprite {
    public:
        Merchant(int id, double x, double y) : Sprite(id, x, y) {}
}; // å•†äººä»ç„¶ç›´æ¥ç»§æ‰¿ Sprite
```

è¿™å½¢æˆäº†ä¸€ä¸ªæ¸…æ™°çš„ç»§æ‰¿é“¾ï¼š`Sprite â†’ Warrior â†’ Hero/Enemy`ï¼Œ`Sprite â†’ Merchant`ã€‚

### Moonbitçš„ç»„åˆå¼å¤šå±‚èƒ½åŠ›

åœ¨Moonbitä¸­ï¼Œæˆ‘ä»¬ç»§ç»­åšæŒç»„åˆçš„æ€è·¯ï¼Œæ„å»ºä¸€ä¸ªæ›´çµæ´»çš„èƒ½åŠ›ä½“ç³»ï¼š

```moonbit no check
pub struct WarriorData {
  hp: Double
  damage: Double
}

// Warrior trait ç»§æ‰¿è‡ª Spriteï¼Œå½¢æˆèƒ½åŠ›å±‚æ¬¡
pub trait Warrior : Sprite {
  getWarriorData(Self) -> WarriorData
  asWarriorEnum(Self) -> WarriorEnum
  attack(Self, target: &Warrior) -> Unit = _  // é»˜è®¤å®ç°
}

pub enum WarriorEnum {
  Hero(Hero)
  Enemy(Enemy)
}

// é‡æ–°å®šä¹‰Heroï¼Œç°åœ¨å®ƒç»„åˆäº†ä¸¤ç§æ•°æ®
pub struct Hero {
  sprite_data: SpriteData    // åŸºç¡€ç²¾çµæ•°æ®
  warrior_data: WarriorData  // æˆ˜å£«æ•°æ®
  money: Int                 // è‹±é›„ç‰¹æœ‰æ•°æ®
}

// Hero éœ€è¦å®ç°å¤šä¸ª trait
pub impl Sprite for Hero with getSpriteData(self) {
  self.sprite_data
}

pub impl Warrior for Hero with getWarriorData(self) {
  self.warrior_data
}

pub impl Warrior for Hero with asWarriorEnum(self) {
  Hero(self)
}

// é‡æ–°å®šä¹‰Enemy
pub struct Enemy {
  sprite_data: SpriteData
  warrior_data: WarriorData
}

pub impl Sprite for Enemy with getSpriteData(self) {
  self.sprite_data
}

pub impl Warrior for Enemy with getWarriorData(self) {
  self.warrior_data
}

pub impl Warrior for Enemy with asWarriorEnum(self) {
  Enemy(self)
}
```

æœ‰æ—¶æˆ‘ä»¬ä¹Ÿå¯èƒ½ä¼šé‡åˆ°éœ€è¦å°†çˆ¶åŸºç±»è½¬æ¢æˆå­åŸºç±»çš„åœºæ™¯ã€‚ä¾‹å¦‚ï¼Œæˆ‘ä»¬çš„å•†äººå¯èƒ½å¯¹ä¸åŒçš„Spriteåšå‡ºä¸åŒçš„ååº”ï¼šå½“ä»–é‡åˆ°ä¸€ä¸ªWarrioræ—¶ï¼Œä»–ä¼šè¯´"Want to buy something?"ï¼Œå½“ä»–é‡åˆ°å¦ä¸€ä¸ªå•†äººæ—¶ï¼Œåˆ™ä»€ä¹ˆä¹Ÿä¸åšã€‚è¿™ä¸ªæ—¶å€™ï¼Œæˆ‘ä»¬å°±éœ€è¦æŠŠSpriteçˆ¶åŸºç±»è½¬æ¢æˆWarriorå­åŸºç±»ã€‚æ¨èçš„æ–¹å¼æ˜¯ä¸º`Sprite` traitæ·»åŠ ä¸€ä¸ª`tryAsWarrior`çš„å‡½æ•°ï¼š

```moonbit no check
pub trait Sprite {
  // other methods
  tryAsWarrior(Self) -> &Warrior? = _  // å°è¯•è½¬æ¢ä¸ºWarrior
}

impl Sprite with tryAsWarrior(self) {
  match self.asSpriteEnum() {
    // ç¬¬ä¸€é¡¹éœ€è¦æ·»åŠ  as &Warrior, æ¥å‘ŠçŸ¥ç¼–è¯‘å™¨æ•´ä¸ªè¡¨è¾¾å¼è¿”å›ä¸€ä¸ª&Warrior
    // å¦‚æœä¸åŠ è¿™ä¸ªasè¯­å¥ï¼Œç¼–è¯‘å™¨å°±ä¼šæ ¹æ®ç¬¬ä¸€ä¸ªè¡¨è¾¾å¼çš„ç±»å‹
    // åˆ¤æ–­æ•´ä¸ªè¡¨è¾¾å¼çš„ç±»å‹ä¸ºHeroï¼Œä»è€Œå¼•å‘ç¼–è¯‘é”™è¯¯ã€‚
    Hero(h) => Some(h as &Warrior)
    Enemy(e) => Some(e)
    _ => None
  }
}

pub fn Merchant::ask(self: Merchant, s: &Sprite) -> String {
  match s.tryAsWarrior() {
    Some(_) => "Want to buy something?"  // å¯¹æˆ˜å£«è¯´è¯
    None => ""                           // å¯¹å…¶ä»–ç±»å‹ä¿æŒæ²‰é»˜
  }
}
```

è¿™ç§è®¾è®¡çš„ç²¾å¦™ä¹‹å¤„åœ¨äºå®ƒçš„**æè‡´çµæ´»æ€§**ï¼š

- `Hero`å’Œ`Enemy`é€šè¿‡**ç»„åˆ**`SpriteData`å’Œ`WarriorData`ï¼ŒåŒæ—¶**å®ç°**`Sprite`å’Œ`Warrior`ä¸¤ä¸ªtraitï¼Œè·å¾—äº†æ‰€éœ€çš„å…¨éƒ¨èƒ½åŠ›
- `Merchant`åªéœ€è¦ç»„åˆ`SpriteData`å¹¶å®ç°`Sprite` traitå³å¯
- å¦‚æœå°†æ¥è¦å¼•å…¥`Mage`ï¼ˆæ³•å¸ˆï¼‰èƒ½åŠ›ï¼Œåªéœ€å®šä¹‰`MageData`å’Œ`Mage` trait
- ä¸€ä¸ªè§’è‰²ç”šè‡³å¯ä»¥åŒæ—¶æ˜¯`Warrior`å’Œ`Mage`ï¼Œæˆä¸º"é­”å‰‘å£«"ï¼Œè€Œä¸éœ€è¦å¤„ç†C++ä¸­çš„è±å½¢ç»§æ‰¿é—®é¢˜

> **è±å½¢ç»§æ‰¿é—®é¢˜**
>
> å‡è®¾æˆ‘ä»¬è¦åˆ›å»ºä¸€ä¸ªæ—¢æ˜¯å•†äººåˆæ˜¯æ•Œäººçš„`Profiteer`ï¼ˆå¥¸å•†ï¼‰ç±»ã€‚åœ¨C++ä¸­ï¼Œå¦‚æœ`Profiteer`åŒæ—¶ç»§æ‰¿`Enemy`å’Œ`Merchant`ï¼Œå°±ä¼šå‡ºç°è±å½¢ç»§æ‰¿ï¼š`Profiteer`ä¼šæ‹¥æœ‰ä¸¤ä»½`Sprite`æ•°æ®ï¼è¿™å¯èƒ½å¯¼è‡´ä¿®æ”¹äº†ä¸€ä»½æ•°æ®ï¼Œä½†è°ƒç”¨æ—¶å´ä½¿ç”¨äº†å¦ä¸€ä»½çš„è¯¡å¼‚bugã€‚Moonbitçš„ç»„åˆæ–¹å¼ä»æ ¹æœ¬ä¸Šé¿å…äº†è¿™ä¸ªé—®é¢˜ã€‚

---

## ä¼ ç»Ÿé¢å‘å¯¹è±¡ç¼–ç¨‹çš„æ·±å±‚é—®é¢˜

çœ‹åˆ°è¿™é‡Œï¼Œä½ å¯èƒ½ä¼šæƒ³ï¼š"Moonbitçš„æ–¹æ³•éœ€è¦å†™æ›´å¤šä»£ç ï¼Œçœ‹èµ·æ¥æ›´å¤æ‚å•Šï¼"ç¡®å®ï¼Œä»ä»£ç è¡Œæ•°æ¥çœ‹ï¼ŒMoonbitä¼¼ä¹éœ€è¦æ›´å¤šçš„"æ¨¡æ¿ä»£ç "ã€‚ä½†æ˜¯ï¼Œåœ¨çœŸå®çš„è½¯ä»¶å·¥ç¨‹å®è·µä¸­ï¼Œä¼ ç»Ÿçš„é¢å‘å¯¹è±¡ç¼–ç¨‹æ–¹å¼å®é™…ä¸Šå­˜åœ¨è¯¸å¤šæ·±å±‚é—®é¢˜ï¼š

### 1. è„†å¼±çš„ç»§æ‰¿é“¾

**é—®é¢˜**ï¼šå¯¹çˆ¶ç±»çš„ä»»ä½•ä¿®æ”¹éƒ½ä¼šå½±å“æ‰€æœ‰å­ç±»ï¼Œå¯èƒ½äº§ç”Ÿéš¾ä»¥é¢„ä¼°çš„è¿é”ååº”ã€‚

æƒ³è±¡ä¸€ä¸‹ä½ çš„RPGæ¸¸æˆå·²ç»å‘å¸ƒäº†ä¸¤å¹´ï¼Œæ‹¥æœ‰ä¸Šç™¾ç§ä¸åŒçš„`Sprite`å­ç±»ã€‚ç°åœ¨ä½ éœ€è¦ç»™åŸºç¡€çš„`Sprite`ç±»åšä¸€ä¸ªé‡æ„ã€‚ç„¶è€Œï¼Œä½ å¯èƒ½å¾ˆå¿«å°±ä¼šå‘ç°è¿™å¹¶ä¸ç°å®ã€‚åœ¨ä¼ ç»Ÿç»§æ‰¿ä½“ç³»ä¸­ï¼Œè¿™ä¸ªæ”¹åŠ¨ä¼šå½±å“åˆ°æ¯ä¸€ä¸ªå­ç±»ï¼Œå³ä¾¿æ˜¯å¾ˆå°çš„æ”¹åŠ¨å¯èƒ½ä¹Ÿå½±å“å·¨å¤§ã€‚æŸäº›å­ç±»å¯èƒ½å› ä¸ºè¿™ä¸ªæ”¹åŠ¨å‡ºç°æ„å¤–çš„è¡Œä¸ºå˜åŒ–ï¼Œè€Œä½ éœ€è¦é€ä¸€æ£€æŸ¥å’Œæµ‹è¯•æ‰€æœ‰ç›¸å…³ä»£ç ã€‚

**Moonbitçš„è§£å†³æ–¹æ¡ˆ**ï¼šç»„åˆå¼è®¾è®¡è®©æˆ‘ä»¬å¯ä»¥é€šè¿‡ADTç›´æ¥æ‰¾åˆ°Spriteçš„æ‰€æœ‰å­ç±»ï¼Œç«‹åˆ»çŸ¥é“é‡æ„ä»£ç çš„å½±å“èŒƒå›´ã€‚

### 2. è±å½¢ç»§æ‰¿çš„å™©æ¢¦

**é—®é¢˜**ï¼šå¤šé‡ç»§æ‰¿å®¹æ˜“å¯¼è‡´è±å½¢ç»§æ‰¿ï¼Œäº§ç”Ÿæ•°æ®é‡å¤å’Œæ–¹æ³•è°ƒç”¨æ­§ä¹‰ã€‚

å¦‚å‰æ‰€è¿°ï¼Œ`Profiteer`ç±»åŒæ—¶ç»§æ‰¿`Enemy`å’Œ`Merchant`æ—¶ï¼Œä¼šæ‹¥æœ‰ä¸¤ä»½`Sprite`æ•°æ®ã€‚è¿™ä¸ä»…æµªè´¹å†…å­˜ï¼Œæ›´å¯èƒ½å¯¼è‡´æ•°æ®ä¸ä¸€è‡´çš„bugã€‚

**Moonbitçš„è§£å†³æ–¹æ¡ˆ**ï¼šç»„åˆå¤©ç„¶é¿å…äº†è¿™ä¸ªé—®é¢˜ï¼Œ`Profiteer`å¯ä»¥æ‹¥æœ‰`SpriteData`ã€`WarriorData`å’Œ`MerchantData`ï¼Œæ¸…æ™°æ˜äº†ã€‚

### 3. è¿è¡Œæ—¶é”™è¯¯çš„éšæ‚£

**é—®é¢˜**ï¼šä¼ ç»ŸOOPçš„è®¸å¤šé—®é¢˜åªèƒ½åœ¨è¿è¡Œæ—¶è¢«å‘ç°ï¼Œå¢åŠ äº†è°ƒè¯•éš¾åº¦å’Œé¡¹ç›®é£é™©ã€‚

è¿˜è®°å¾—å‰é¢`dynamic_cast`çš„ä¾‹å­å—ï¼Ÿå¦‚æœä½ æ·»åŠ äº†æ–°çš„å­ç±»ä½†å¿˜è®°æ›´æ–°ç›¸å…³çš„ç±»å‹åˆ¤æ–­ä»£ç ï¼Œåªæœ‰åœ¨ç¨‹åºè¿è¡Œåˆ°é‚£ä¸ªåˆ†æ”¯æ—¶æ‰ä¼šæš´éœ²é—®é¢˜ã€‚åœ¨å¤§å‹é¡¹ç›®ä¸­ï¼Œè¿™å¯èƒ½æ„å‘³ç€bugåœ¨ç”Ÿäº§ç¯å¢ƒä¸­æ‰è¢«å‘ç°ã€‚

**Moonbitçš„è§£å†³æ–¹æ¡ˆ**ï¼šADTé…åˆæ¨¡å¼åŒ¹é…æä¾›ç¼–è¯‘æ—¶ç±»å‹å®‰å…¨ã€‚é—æ¼ä»»ä½•ä¸€ä¸ªcaseï¼Œç¼–è¯‘å™¨éƒ½ä¼šæŠ¥é”™ã€‚

### 4. å¤æ‚åº¦çˆ†ç‚¸

**é—®é¢˜**ï¼šæ·±å±‚ç»§æ‰¿æ ‘å˜å¾—éš¾ä»¥ç†è§£å’Œç»´æŠ¤ã€‚

ç»è¿‡å‡ å¹´çš„å¼€å‘ï¼Œä½ çš„æ¸¸æˆå¯èƒ½æ¼”åŒ–å‡ºè¿™æ ·çš„ç»§æ‰¿æ ‘ï¼š

```
Sprite
â”œâ”€â”€ Warrior
â”‚   â”œâ”€â”€ Hero
â”‚   â”‚   â”œâ”€â”€ Paladin
â”‚   â”‚   â”œâ”€â”€ Berserker
â”‚   â”‚   â””â”€â”€ ...
â”‚   â””â”€â”€ Enemy
â”‚       â”œâ”€â”€ Orc
â”‚       â”œâ”€â”€ Dragon
â”‚       â””â”€â”€ ...
â”œâ”€â”€ Mage
â”‚   â”œâ”€â”€ Wizard
â”‚   â””â”€â”€ Sorceror
â””â”€â”€ NPC
    â”œâ”€â”€ Merchant
    â”œâ”€â”€ QuestGiver
    â””â”€â”€ ...
```

å½“éœ€è¦é‡æ„æ—¶ï¼Œä½ å¯èƒ½éœ€è¦èŠ±è´¹å¤§é‡æ—¶é—´æ¥ç†è§£è¿™ä¸ªå¤æ‚çš„ç»§æ‰¿å…³ç³»ï¼Œè€Œä¸”ä»»ä½•æ”¹åŠ¨éƒ½å¯èƒ½äº§ç”Ÿæ„æƒ³ä¸åˆ°çš„å‰¯ä½œç”¨ã€‚

**Moonbitçš„è§£å†³æ–¹æ¡ˆ**ï¼šæ‰å¹³åŒ–çš„ç»„åˆç»“æ„è®©ç³»ç»Ÿæ›´å®¹æ˜“ç†è§£ã€‚æ¯ä¸ªèƒ½åŠ›éƒ½æ˜¯ç‹¬ç«‹çš„traitï¼Œç»„åˆå…³ç³»ä¸€ç›®äº†ç„¶ã€‚

## ç»“è¯­

é€šè¿‡è¿™æ¬¡æ·±å…¥çš„æ¯”è¾ƒï¼Œæˆ‘ä»¬çœ‹åˆ°äº†ä¸¤ç§æˆªç„¶ä¸åŒçš„é¢å‘å¯¹è±¡ç¼–ç¨‹å“²å­¦ï¼š

- **C++çš„ä¼ ç»ŸOOP**ï¼šåŸºäºç»§æ‰¿çš„"is-a"å…³ç³»ï¼Œç›´è§‚ä½†å¯èƒ½é™·å…¥å¤æ‚åº¦é™·é˜±
- **Moonbitçš„ç°ä»£OOP**ï¼šåŸºäºç»„åˆçš„"has-a"å…³ç³»ï¼Œåˆå­¦ç¨å¤æ‚ä½†é•¿æœŸæ›´ä¼˜é›…

Moonbitçš„æ–¹æ³•è™½ç„¶éœ€è¦ç¼–å†™æ›´å¤šçš„"æ¨¡æ¿ä»£ç "ï¼Œä½†è¿™äº›é¢å¤–çš„ä»£ç æ¢æ¥çš„æ˜¯ï¼š

- **æ›´å¥½çš„ç±»å‹å®‰å…¨**ï¼šç¼–è¯‘æ—¶æ•è·æ›´å¤šé”™è¯¯
- **æ›´æ¸…æ™°çš„æ¶æ„**ï¼šç»„åˆå…³ç³»æ¯”ç»§æ‰¿å…³ç³»æ›´å®¹æ˜“ç†è§£
- **æ›´å®¹æ˜“çš„ç»´æŠ¤**ï¼šä¿®æ”¹å½±å“èŒƒå›´æ›´å¯æ§
- **æ›´å°‘çš„è¿è¡Œæ—¶é”™è¯¯**ï¼šADTå’Œæ¨¡å¼åŒ¹é…æä¾›å®Œæ•´æ€§ä¿è¯

å°½ç®¡æˆ‘ä»¬å¿…é¡»æ‰¿è®¤ï¼Œå¯¹äºå°å‹é¡¹ç›®æˆ–ç‰¹å®šåœºæ™¯ï¼Œä¼ ç»Ÿç»§æ‰¿ä¾ç„¶æœ‰å…¶ä»·å€¼ã€‚ä½†ç°å®æƒ…å†µæ˜¯ï¼Œéšç€è½¯ä»¶ç³»ç»Ÿå¤æ‚åº¦çš„å¢é•¿ï¼ŒMoonbitè¿™ç§ç»„åˆä¼˜äºç»§æ‰¿çš„è®¾è®¡å“²å­¦ç¡®å®å±•ç°å‡ºäº†æ›´å¼ºçš„é€‚åº”æ€§å’Œå¯ç»´æŠ¤æ€§ã€‚

å¸Œæœ›è¿™ç¯‡æ–‡ç« èƒ½ä¸ºä½ çš„Moonbitç¼–ç¨‹ä¹‹æ—…æä¾›æœ‰ä»·å€¼çš„æŒ‡å¯¼ï¼Œè®©ä½ åœ¨æ„å»ºå¤æ‚ç³»ç»Ÿæ—¶èƒ½å¤Ÿå……åˆ†åˆ©ç”¨Moonbitçš„è®¾è®¡ä¼˜åŠ¿ã€‚

---

## å®Œæ•´ç‰ˆä»£ç 

```moonbit
pub struct SpriteData {
  id: Int
  mut x: Double
  mut y: Double
}

pub fn SpriteData::new(id: Int, x: Double, y: Double) -> SpriteData {
  SpriteData::{ id, x, y }
}

// 2. å®šä¹‰æè¿°é€šç”¨è¡Œä¸ºçš„ Trait
pub trait Sprite {
  getSpriteData(Self) -> SpriteData
  asSpriteEnum(Self) -> SpriteEnum
  tryAsWarrior(Self) -> &Warrior? = _
  getID(Self) -> Int = _
  getX(Self) -> Double = _
  getY(Self) -> Double = _
  setX(Self, Double) -> Unit = _
  setY(Self, Double) -> Unit = _
}

// Spriteçš„é»˜è®¤å®ç°
// åªè¦å®ç°äº† getSpriteDataï¼Œå°±è‡ªåŠ¨æ‹¥æœ‰äº†å…¶ä»–æ–¹æ³•
impl Sprite with getID(self) {
  self.getSpriteData().id
}

impl Sprite with getX(self) {
  self.getSpriteData().x
}

impl Sprite with getY(self) {
  self.getSpriteData().y
}

impl Sprite with setX(self, new_x) {
  self.getSpriteData().x = new_x
}

impl Sprite with setY(self, new_y) {
  self.getSpriteData().y = new_y
}

impl Sprite with tryAsWarrior(self) {
  match self.asSpriteEnum() {
    Hero(h) => Some(h as &Warrior)
    Enemy(e) => Some(e)
    _ => None
  }
}

pub enum SpriteEnum {
  Hero(Hero)
  Enemy(Enemy)
  Merchant(Merchant)
}

pub struct WarriorData {
  hp: Double
  damage: Double
}

pub trait Warrior : Sprite {  // Warrior ç»§æ‰¿è‡ª Sprite
  getWarriorData(Self) -> WarriorData
  asWarriorEnum(Self) -> WarriorEnum
  attack(Self, target: &Warrior) -> Unit = _
}

impl Warrior with attack(self, target) {
  ignore((self, target))
  // ...
}

pub enum WarriorEnum {
  Hero(Hero)
  Enemy(Enemy)
}

// å®šä¹‰Hero
pub struct Hero {
  sprite_data: SpriteData
  warrior_data: WarriorData
  money: Int
}

pub fn Hero::new(
) -> Hero {
  let sprite_data = SpriteData::new(0, 42, 33)
  let warrior_data = WarriorData::{ hp: 100, damage: 20 }
  Hero::{sprite_data, warrior_data, money: 1000}
}

pub impl Sprite for Hero with getSpriteData(self) {
  self.sprite_data
}

pub impl Sprite for Hero with asSpriteEnum(self) {
  Hero(self)
}

pub impl Warrior for Hero with getWarriorData(self) {
  self.warrior_data
}

pub impl Warrior for Hero with asWarriorEnum(self) {
  WarriorEnum::Hero(self)
}

// å®šä¹‰Enemy
pub struct Enemy {
  sprite_data: SpriteData
  warrior_data: WarriorData
}

pub fn Enemy::new() -> Enemy {
  let sprite_data = SpriteData::new(0, 42, 33)
  let warrior_data = WarriorData::{ hp: 100, damage: 5}
  Enemy::{sprite_data, warrior_data}
}

pub impl Sprite for Enemy with getSpriteData(self) {
  self.sprite_data
}

pub impl Sprite for Enemy with asSpriteEnum(self) {
  Enemy(self)
}

pub impl Warrior for Enemy with getWarriorData(self) {
  self.warrior_data
}

pub impl Warrior for Enemy with asWarriorEnum(self) {
  WarriorEnum::Enemy(self)
}

// å®šä¹‰Merchant
pub struct Merchant {
  sprite_data: SpriteData
}

pub fn Merchant::new() -> Merchant {
  let sprite_data = SpriteData::new(0, 42, 33)
  Merchant::{sprite_data}
}

pub impl Sprite for Merchant with getSpriteData(self) {
  self.sprite_data
}

pub impl Sprite for Merchant with asSpriteEnum(self) {
  Merchant(self)
}

pub fn Merchant::ask(self: Merchant, s: &Sprite) -> String {
  ignore(self)
  match s.tryAsWarrior() {
    Some(_) =>"what to buy something?"
    None => ""
  }
}

test "who are you" {
  fn who_are_you(s: &Sprite) -> String {
    match s.asSpriteEnum() {
      Hero(_) => "hero"
      Enemy(_) => "enemy"
      Merchant(_) => "merchant"
    }
  }
  let hero = Hero::new();
  let enemy = Enemy::new();
  let merchant = Merchant::new();
  inspect(who_are_you(hero), content="hero")
  inspect(who_are_you(enemy), content="enemy")
  inspect(who_are_you(merchant), content="merchant")
}
pub trait SayName {
  say_name(Self) -> String
}

pub impl SayName for Hero with say_name(_) {
  "hero"
}

pub impl SayName for Enemy with say_name(_) {
  "enemy"
}

pub impl SayName for Merchant with say_name(_) {
  "merchant"
}

test "say_name" {
  fn who_are_you(s: &SayName) -> String {
    s.say_name()
  }

  let hero = Hero::new();
  let enemy = Enemy::new();
  let merchant = Merchant::new();
  inspect(who_are_you(hero), content="hero")
  inspect(who_are_you(enemy), content="enemy")
  inspect(who_are_you(merchant), content="merchant")
}

test "merchant ask" {
  let hero = Hero::new();
  let enemy = Enemy::new();
  let merchant = Merchant::new();

  inspect(merchant.ask(hero), content="what to buy something?")
  inspect(merchant.ask(enemy), content="what to buy something?")
  inspect(merchant.ask(merchant), content="")
}
```
